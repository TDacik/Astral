(* Output to smtlib format
 *
 * Author: Tomas Dacik (xdacik00@fit.vutbr.cz), 2022 *)

open SSL

module F = Format

let header =
"; Generated by Astral\n
(set-logic QF_ALL_SUPPORTED)\n
(declare-sort Loc 0)\n"

let translate_var var = match var with
  | Variable.Var _ -> Variable.show var
  | Variable.Nil -> "(as nil Loc)"

let translate_vars phi =
  SSL.get_vars phi
  |> List.filter (fun v -> not @@ SSL.Variable.is_nil v)
  |> List.sort_uniq SSL.Variable.compare
  |> List.map translate_var
  |> List.map (F.asprintf "(declare-const %s Loc)")
  |> String.concat "\n"

let rec translate = function
  | And (f1, f2) -> F.asprintf "(and %s %s)\n" (translate f1) (translate f2)
  | Or (f1, f2) -> F.asprintf "(or %s %s)\n" (translate f1) (translate f2)
  | Not f ->  F.asprintf "(not %s)\n" (translate f)
  | GuardedNeg (f1, f2) ->  F.asprintf "(and %s (not %s))\n" (translate f1) (translate f2)
  | Star (f1, f2) ->  F.asprintf "(sep %s %s)\n" (translate f1) (translate f2)
  | LS (v1, v2) -> F.asprintf "(ls %s %s)\n" (translate_var v1) (translate_var v2)
  | PointsTo (v1, v2) -> F.asprintf "(pto %s %s)\n" (translate_var v1) (translate_var v2)
  | Eq (v1, v2) -> F.asprintf "(= %s %s)\n" (translate_var v1) (translate_var v2)
  | Neq (v1, v2) -> F.asprintf "(distinct %s %s)\n" (translate_var v1) (translate_var v2)
  | Septraction (f1, f2) -> F.asprintf "(not (wand %s (not %s)))\n"
      (translate f1) (translate f2)

let translate_all phi =
  header
  ^ (translate_vars phi) ^ "\n\n(assert " ^ (translate phi) ^ ")\n"
  ^ "(check-sat)"

let dump file phi =
  let channel = open_out_gen [Open_creat; Open_wronly] 0o666 file in
  Printf.fprintf channel "%s\n" (translate_all phi);
  close_out channel
